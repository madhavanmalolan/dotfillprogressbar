apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'



android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.android.support:appcompat-v7:25.4.0'
    testImplementation 'junit:junit:4.12'
}





task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.madhavanmalolan.android'
version = '0.1.2'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'dotfillprogress'
            description 'A progressbar that fills empty dots'
            url 'https://github.com/madhavanmalolan/dotfillprogressbar'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'com.madhavanmalolan.android'
            artifactId 'dotfillprogress'
            version '0.1.2'

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            scm {
                connection 'https://github.com/madhavanmalolan/dotfillprogressbar.git'
                url 'https://github.com/madhavanmalolan/dotfillprogressbar'

            }
            developers {
                developer {
                    id = 'madhavanmalolan'
                    name 'Madhavan'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'dotfillprogress'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/madhavanmalolan/dotfillprogressbar.git'
        publish = true
        version {
            name = '0.1.2'
            desc = 'dotfillprogress 0.1.2'
            released = new Date()
            vcsTag = 'v0.1.2'
        }
    }
}